--
-- OC_RENOVACION  (Package) 
--
--  Dependencies: 
--   STANDARD (Package)
--   STANDARD (Package)
--   POLIZAS (Table)
--   FACTURAS (Table)
--   DETALLE_POLIZA (Table)
--   EXTRAE_CLAUSULA_POL (Function)
--   AGENTE_POLIZA (Table)
--   ASEGURADO (Table)
--   TRANSACCION (Table)
--   OC_SOLICITUD_EMISION (Package)
--   SOLICITUDES_CLAUSULAS (Table)
--   SOLICITUD_DETALLE (Table)
--   SOLICITUD_EMISION (Table)
--   CLAUSULAS_POLIZA (Table)
--   CLIENTES (Table)
--   OC_FILIALES (Package)
--   OC_GENERALES (Package)
--
CREATE OR REPLACE PACKAGE SICAS_OC.OC_RENOVACION IS

FUNCTION MIGRA_A_AUTOFACIL(P_POLIZA IN  NUMBER,
                           MENSAJE  OUT VARCHAR2) RETURN NUMBER;

END OC_RENOVACION;
/

--
-- OC_RENOVACION  (Package Body) 
--
--  Dependencies: 
--   OC_RENOVACION (Package)
--
CREATE OR REPLACE PACKAGE BODY SICAS_OC.OC_RENOVACION IS
 --
 -- CREADO 20/06/2016
 --
 W_CODCIA                   POLIZAS.CODCIA%TYPE;
 W_CODEMPRESA               POLIZAS.CODEMPRESA%TYPE;
 W_IDPOLIZA                 POLIZAS.IDPOLIZA%TYPE;
 W_PLANCOB                  DETALLE_POLIZA.PLANCOB%TYPE;
 W_COD_MONEDA               POLIZAS.COD_MONEDA%TYPE;
 W_IDTIPOSEG                DETALLE_POLIZA.IDTIPOSEG%TYPE;
 W_TIPO_DOC_CONTR           CLIENTES.TIPO_DOC_IDENTIFICACION%TYPE;
 W_NUM_DOC_CONTR            CLIENTES.NUM_DOC_IDENTIFICACION%TYPE;
 W_FECINIVIG                POLIZAS.FECINIVIG%TYPE;
 W_FECFINVIG                POLIZAS.FECFINVIG%TYPE;
 W_CODPLANPAGO              POLIZAS.CODPLANPAGO%TYPE;
 W_TIPOADMINISTRACION       POLIZAS.TIPOADMINISTRACION%TYPE;
 W_CODAGRUPADOR             POLIZAS.CODAGRUPADOR%TYPE;
 W_INDFACTURAPOL            POLIZAS.INDFACTURAPOL%TYPE;
 W_NUMPOLUNICO              POLIZAS.NUMPOLUNICO%TYPE;
 W_INDASEGMODELO            DETALLE_POLIZA.INDASEGMODELO%TYPE;
 W_PRIMANETA_LOCAL          POLIZAS.PRIMANETA_LOCAL%TYPE;
 W_DESCPOLIZA               POLIZAS.DESCPOLIZA%TYPE;
 W_INDFACTELECTRONICA       POLIZAS.INDFACTELECTRONICA%TYPE;
 W_INDCALCDERECHOEMIS       POLIZAS.INDCALCDERECHOEMIS%TYPE;
 W_CODDIRECREGIONAL         POLIZAS.CODDIRECREGIONAL%TYPE;
 W_NUMFOLIOPORTAL           POLIZAS.NUMFOLIOPORTAL%TYPE;
 W_TIPO_DOC_ASEG            ASEGURADO.TIPO_DOC_IDENTIFICACION%TYPE;
 W_NUM_DOC_ASEG             ASEGURADO.NUM_DOC_IDENTIFICACION%TYPE;
 W_CODUSUARIO               SOLICITUD_EMISION.CODUSUARIO%TYPE; 
 W_IDSOLICITUD              SOLICITUD_EMISION.IDSOLICITUD%TYPE;
 W_TASACAMBIO               SOLICITUD_EMISION.TASACAMBIO%TYPE;
 W_CODGRUPOEC               POLIZAS.CODGRUPOEC%TYPE;
  --
 MENSAJE                    VARCHAR2(2000);
--
-- MIGRA POLIZAS DE BASES DE EMISION A SOLICITUD
--
CURSOR DETALLE IS
  SELECT DP.IDETPOL,
         DP.PRIMA_LOCAL,
         DP.CANTASEGMODELO,
         DP.CODFILIAL CODSUBGRUPO,
         SUBSTR(OC_FILIALES.NOMBRE_FILIAL(W_CODCIA,W_CODGRUPOEC,DP.CODFILIAL),1,48) DS_SUBGRUPO
    FROM DETALLE_POLIZA DP
   WHERE DP.IDPOLIZA = W_IDPOLIZA
  ;
--
-- MIGRA CLAUSULAS DE BASES DE EMISION A SOLICITUD
--
CURSOR CLAUSULA IS
  SELECT CODCIA,
         IDPOLIZA,
         COD_CLAUSULA,
         TIPO_CLAUSULA,
         SUBSTR(EXTRAE_CLAUSULA_POL(ROWID),1,3998) TEXTO,
         INICIO_VIGENCIA,
         FIN_VIGENCIA,
         ESTADO
    FROM CLAUSULAS_POLIZA CP
   WHERE CP.IDPOLIZA = W_IDPOLIZA
  ;  
--
--
FUNCTION MIGRA_A_AUTOFACIL(P_POLIZA IN  NUMBER,
                           MENSAJE  OUT VARCHAR2) RETURN NUMBER IS
--
BEGIN
  --
  -- ACTUALIZA TABLAS DE SOLICITUD
  --
  SELECT P.CODCIA,        
         P.CODEMPRESA,
         P.IDPOLIZA,        
         P.COD_MONEDA,
         ADD_MONTHS(P.FECINIVIG,12),
         ADD_MONTHS(P.FECFINVIG,12),
         --
         P.CODPLANPAGO,
         P.TIPOADMINISTRACION,
         P.CODAGRUPADOR,
         P.INDFACTURAPOL,
         P.NUMPOLUNICO,
         P.PRIMANETA_LOCAL,
         --
         P.DESCPOLIZA,
         P.INDFACTELECTRONICA,
         P.INDCALCDERECHOEMIS,
         P.CODDIRECREGIONAL,
         P.NUMFOLIOPORTAL,
         P.CODGRUPOEC,
         --
         DP.IDTIPOSEG,
         DP.PLANCOB,
         DP.INDASEGMODELO,
         --
         C.TIPO_DOC_IDENTIFICACION,
         C.NUM_DOC_IDENTIFICACION,
         --
         A.TIPO_DOC_IDENTIFICACION,
         A.NUM_DOC_IDENTIFICACION
    INTO W_CODCIA,    
         W_CODEMPRESA,
         W_IDPOLIZA, 
         W_COD_MONEDA, 
         W_FECINIVIG,
         W_FECFINVIG,
         --
         W_CODPLANPAGO,
         W_TIPOADMINISTRACION,
         W_CODAGRUPADOR,
         W_INDFACTURAPOL,
         W_NUMPOLUNICO,
         W_PRIMANETA_LOCAL,
         --
         W_DESCPOLIZA,
         W_INDFACTELECTRONICA,
         W_INDCALCDERECHOEMIS,
         W_CODDIRECREGIONAL,
         W_NUMFOLIOPORTAL,
         W_CODGRUPOEC,
         --
         W_IDTIPOSEG,
         W_PLANCOB,
         W_INDASEGMODELO,
         --
         W_TIPO_DOC_CONTR,
         W_NUM_DOC_CONTR,
         --
         W_TIPO_DOC_ASEG,
         W_NUM_DOC_ASEG
    FROM POLIZAS        P,    
         DETALLE_POLIZA DP,    
         CLIENTES       C,
         ASEGURADO      A,
         AGENTE_POLIZA  AP
   WHERE P.IDPOLIZA = P_POLIZA     
     --
     AND DP.IDPOLIZA = P.IDPOLIZA
     AND DP.IDETPOL  = (SELECT MIN(DPI.IDETPOL)          
                          FROM DETALLE_POLIZA DPI
                         WHERE DPI.IDPOLIZA = P.IDPOLIZA)
     --
     AND C.CODCLIENTE = P.CODCLIENTE        
     --
     AND A.COD_ASEGURADO = DP.COD_ASEGURADO  
     --
     AND AP.IDPOLIZA = P.IDPOLIZA            
  ;
  --
  --  EXTRAE EL USUARIO
  --
  BEGIN
    SELECT T.USUARIOGENERO
      INTO W_CODUSUARIO
      FROM POLIZAS     P,
           FACTURAS    F,
           TRANSACCION T
     WHERE P.IDPOLIZA = P_POLIZA
       --
       AND F.CODCIA    = P.CODCIA
       AND F.IDPOLIZA  = P.IDPOLIZA
       AND F.IDFACTURA = (SELECT MIN(F.IDFACTURA)
                            FROM FACTURAS F
                           WHERE F.IDPOLIZA = P.IDPOLIZA)
       --
      AND T.IDTRANSACCION = F.IDTRANSACCION
      ;
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
         W_CODUSUARIO := 0;
    WHEN OTHERS THEN
         W_CODUSUARIO := 0;
  END;
  --
  W_IDSOLICITUD := OC_SOLICITUD_EMISION.NUMERO_SOLICITUD(W_CODCIA, W_CODEMPRESA);
  W_TASACAMBIO  := OC_GENERALES.TASA_DE_CAMBIO(W_COD_MONEDA, TRUNC(SYSDATE));
  --
  -- INSERTA SOLICITUD_EMISION
  --
  INSERT INTO SOLICITUD_EMISION
   (CODCIA,
    CODEMPRESA,
    IDSOLICITUD,
    NUMCOTIZACION,
    IDPOLIZA,
    IDTIPOSEG,
    PLANCOB,
    COD_MONEDA,
    TASACAMBIO,
    TIPO_DOC_IDENTIFICACION,
    NUM_DOC_IDENTIFICACION,
    FECINIVIG,
    FECFINVIG,
    CODPLANPAGO,
    TIPOADMINISTRACION,
    CODAGRUPADOR,
    INDFACTURAPOL,
    INDFACTSUBGRUPO,
    STSSOLICITUD,
    FECSTSSOL,
    CODUSUARIO,
    NUMPOLUNICOASIG,
    INDASEGMODELO,
    PRIMANETAPOL,
    DESCSOLICITUD,
    INDFACTELECTRONICA,
    INDCALCDERECHOEMIS,
    CODDIRECREGIONAL,
    NUMFOLIOPORTAL,
    TIPODOCIDENTIFASEG,
    NUMDOCIDENTIFASEG
   )
   VALUES
   (W_CODCIA,              -- CODCIA,
    W_CODEMPRESA,          -- CODEMPRESA,
    W_IDSOLICITUD,         -- IDSOLICITUD,
    W_IDPOLIZA,            -- NUMCOTIZACION,
    0,                     -- IDPOLIZA,
    W_IDTIPOSEG,           -- IDTIPOSEG,
    W_PLANCOB,             -- PLANCOB,
    W_COD_MONEDA,          -- COD_MONEDA,
    W_TASACAMBIO,          -- TASACAMBIO,
    W_TIPO_DOC_CONTR,      -- TIPO_DOC_IDENTIFICACION,
    W_NUM_DOC_CONTR,       -- NUM_DOC_IDENTIFICACION,
    W_FECINIVIG,           -- FECINIVIG,
    W_FECFINVIG,           -- FECFINVIG,
    W_CODPLANPAGO,         -- CODPLANPAGO,
    W_TIPOADMINISTRACION,  -- TIPOADMINISTRACION,
    W_CODAGRUPADOR,        -- CODAGRUPADOR,
    W_INDFACTURAPOL,       -- INDFACTURAPOL,
    'N',                   -- INDFACTSUBGRUPO,
    'XENVIA',              -- STSSOLICITUD,
    TRUNC(SYSDATE),        -- FECSTSSOL,
    W_CODUSUARIO,          -- CODUSUARIO,
    W_NUMPOLUNICO,         -- NUMPOLUNICOASIG,
    W_INDASEGMODELO,       -- INDASEGMODELO,
    W_PRIMANETA_LOCAL,     -- PRIMANETAPOL,
    W_DESCPOLIZA,          -- DESCSOLICITUD,
    W_INDFACTELECTRONICA,  -- INDFACTELECTRONICA,
    W_INDCALCDERECHOEMIS,  -- INDCALCDERECHOEMIS,
    W_CODDIRECREGIONAL,    -- CODDIRECREGIONAL,
    'RENOVACION',          -- NUMFOLIOPORTAL,
    W_TIPO_DOC_ASEG,       -- TIPODOCIDENTIFASEG,
    W_NUM_DOC_ASEG         -- NUMDOCIDENTIFASEG
   )
  ;
  --
  --INSERTA DETALLE
  --
  FOR D IN DETALLE LOOP 
      --
      INSERT INTO SOLICITUD_DETALLE 
       (CODCIA,
        CODEMPRESA,
        IDSOLICITUD,
        IDETSOL,
        CODSUBGRUPO,
        DESCSUBGRUPO,
        EDADLIMITE,
        PRIMAASEGURADO,
        CANTASEGMODELO
       )
      VALUES
       (W_CODCIA,
        W_CODEMPRESA,
        W_IDSOLICITUD,
        D.IDETPOL,
        D.CODSUBGRUPO,
        D.DS_SUBGRUPO,
        69,
        D.PRIMA_LOCAL,
        D.CANTASEGMODELO
       );
      --
  END LOOP;
  --
  --INSERTA SOLICITUDES
  --
  FOR C IN CLAUSULA LOOP 
      --
      INSERT INTO SOLICITUDES_CLAUSULAS 
       (CODCIA,
        IDPOLIZA,
        COD_CLAUSULA,
        TIPO_CLAUSULA,
        TEXTO,
        INICIO_VIGENCIA,
        FIN_VIGENCIA,
        ESTADO
       )
      VALUES
       (C.CODCIA,
        W_IDSOLICITUD,
        C.COD_CLAUSULA,
        C.TIPO_CLAUSULA,
        C.TEXTO,
        W_FECINIVIG,
        W_FECFINVIG,
        'SOL'
       );
      --
  END LOOP;
  --
  RETURN W_IDSOLICITUD;
  --
--      COMMIT;
  --
EXCEPTION
 WHEN OTHERS THEN
      MENSAJE := 'ERROR AL MIGRAR LA POLIZA : '||P_POLIZA;    
      RETURN 0;  
END;

END OC_RENOVACION;
/
